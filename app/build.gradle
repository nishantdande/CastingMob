apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    useLibrary 'org.apache.http.legacy'

    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }


    defaultConfig {
        applicationId "com.castingmob"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors
        generatedDensities = []
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            applicationIdSuffix ".debug"
        }
    }

    dataBinding {
        enabled = true
    }

    // Flag to tell aapt to keep the attribute ids around
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    productFlavors{
        Properties props = new Properties()
        castingMob {
            File propsFile = new File('app/src/castingMob/casting.properties')
            props.load(propsFile.newDataInputStream())
            buildConfigField("String", "ENV", "\""+ props.getProperty('casting.baseurl')+"\"")
            applicationId "com.castingmob"
            versionCode = VERSION_CODE
            versionName VERSION_CODE_NAME
        }
        appTest {
            File propsFile = new File('app/src/appTest/casting.properties')
            props.load(propsFile.newDataInputStream())
            buildConfigField("String", "ENV", "\""+ props.getProperty('casting.baseurl')+"\"")
            applicationId "com.castingmob.test"
            versionCode = VERSION_CODE
            versionName VERSION_CODE_NAME
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(output.outputFile.name, "CastingMOB-${variant.versionName}.apk"))
        }
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'

    // Logger Library
    // http://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.loopj.android:android-async-http:1.4.9'
    // Retrofit Network Library Dependency
    // Git - https://github.com/square/retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'info.hoang8f:android-segmented:1.0.6'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    // Dependencies for Slider
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.slider:library:1.1.5@aar'
    compile ('org.apache.httpcomponents:httpcore:4.4.1'){
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile ('org.apache.httpcomponents:httpmime:4.2.5'){
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    // Bottom Bar Tab android
    // Git - https://github.com/roughike/BottomBar
    compile 'com.roughike:bottom-bar:1.4.0.1'

    // Event Bus
    // Git - https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.cocosw:bottomsheet:1.+@aar'
//    compile "org.projectlombok:lombok:1.16.10"

//    compile 'org.simpleframework:simple-xml:2.7.1'

    // Retrofit XML converter (Simple)
    compile ('com.squareup.retrofit2:converter-simplexml:2.1.0'){
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'stax', module: 'stax'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

task clearAppDataCache(type: Exec) {
    description = "Clears device app data/cache."
    group = "Utils"
    def adb = "/Users/nishant/Documents/android_tools/android-sdk-macosx/platform-tools/adb"
    commandLine "$adb", 'shell', 'pm', 'clear', 'com.castingmob.test'
}
